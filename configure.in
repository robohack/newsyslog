dnl
dnl Process this file with autoconf to produce a configure script.
dnl

dnl Copyright (C) 1997 Planix, Inc. - All rights reserved.

AC_INIT(newsyslog.c)
AC_PREREQ(2.12)dnl Required Autoconf version.

AC_REVISION([#ident "@(#)newsyslog:$Name:  $:$Id: configure.in,v 1.11 1998/03/15 20:44:57 woods Exp $"])dnl

PACKAGE=newsyslog
VERSION=1.0.92-Pre

AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)

AM_SANITY_CHECK

AC_ARG_PROGRAM

AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_LANG_C

AC_PROG_MAKE_SET

AM_PROG_INSTALL

missing_dir=`cd $ac_aux_dir && pwd`
AM_MISSING_PROG(ACLOCAL, aclocal, $missing_dir)
AM_MISSING_PROG(AUTOCONF, autoconf, $missing_dir)
AM_MISSING_PROG(AUTOMAKE, automake, $missing_dir)
AM_MISSING_PROG(AUTOHEADER, autoheader, $missing_dir)
AM_MISSING_PROG(MAKEINFO, makeinfo, $missing_dir)

dnl Try to get a POSIX.1 environment.
dnl we need to call these system-specific macros early...
AC_AIX
AC_MINIX
AC_ISC_POSIX

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(errno.h string.h sys/types.h unistd.h)

AC_MSG_CHECKING(for sys/cdefs.h)
AC_TRY_CPP([<sys/cdefs.h>], fd_cv_have_sys_cdefs_h=yes, fd_cv_have_sys_cdefs_h=no)
AC_MSG_RESULT($fd_cv_have_sys_cdefs_h)
if test $fd_cv_have_sys_cdefs_h = no ; then
	# ideally this would be '-I$(srcdir)/compat'
	CPPFLAGS="-Icompat $CPPFLAGS"
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

AC_TYPE_SIGNAL

dnl Checks for library functions.
AC_REPLACE_FUNCS(strdup strerror strtok)
AC_CHECK_FUNCS(strchr)

AC_MSG_CHECKING(for name of archive compression program)
AC_ARG_WITH(gzip,
	[  --with-gzip             use GNU Zip instead of compress],
	[COMPRESS_PROG=gzip
	AC_DEFINE(COMPRESS_POSTFIX, ".gz")],
	[COMPRESS_PROG=compress
	AC_DEFINE(COMPRESS_POSTFIX, ".Z")]
)dnl
AC_MSG_RESULT($COMPRESS_PROG)
AC_SUBST(COMPRESS_PROG, $COMPRESS_PROG)
AC_PATH_PROG(PATH_COMPRESS, $COMPRESS_PROG)
AC_DEFINE_UNQUOTED(PATH_COMPRESS, "$PATH_COMPRESS")

AC_MSG_CHECKING(for name of syslogd's pid file)
case "$localstatedir" in
/*)
	expandedlocalstatedir=$localstatedir
	;;
*)
	oldprefix=$prefix
	if test "x$prefix" = xNONE ; then
		prefix=$ac_default_prefix
	fi
	expandedlocalstatedir=`eval echo $localstatedir`
	prefix=$oldprefix
	;;
esac
AC_ARG_WITH(syslog_pid,
	[  --with-syslog_pid=path syslogd's PID file ($expandedlocalstatedir/run/syslog.pid)],
	[syslog_pidfile=$withval],
	[syslog_pidfile=$expandedlocalstatedir/run/syslog.pid]
)dnl
AC_SUBST(syslog_pidfile)
AC_DEFINE_UNQUOTED(PATH_SYSLOG_PIDFILE, "$syslog_pidfile")
AC_MSG_RESULT($syslog_pidfile)

AC_MSG_CHECKING(for name of config file)
case "$sysconfdir" in
/*)
	expandedsysconfdir=$sysconfdir
	;;
*)
	oldprefix=$prefix
	if test "x$prefix" = xNONE ; then
		prefix=$ac_default_prefix
	fi
	expandedsysconfdir=`eval echo $sysconfdir`
	prefix=$oldprefix
	;;
esac
AC_ARG_WITH(newsyslog_conf,
	[  --with-newsyslog_conf=path configuration file ($expandedsysconfdir/newsyslog.conf)],
	[path_config=$withval],
	[path_config=$expandedsysconfdir/newsyslog.conf]
)dnl
AC_SUBST(path_config)
AC_DEFINE_UNQUOTED(PATH_CONFIG, "$path_config")
AC_MSG_RESULT($path_config)

AC_OUTPUT([Makefile Makefile.BSD newsyslog.conf.5 newsyslog.8])
