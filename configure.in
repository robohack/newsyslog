dnl
dnl Process this file with autoconf to produce a configure script.
dnl

dnl This file:  Copyright (C) 1997,1998,1999,2000,2001,2002 Planix, Inc.
dnl             - see COPYING for details

AC_INIT([NewSyslog], [0.0.0.0-Autoconf-broken], [woods-newsyslog@robohack.planix.com], [newsyslog])

dnl NOTICE:  We must use the obsolete forms of the next two macros
dnl with Autoconf-2.53 because that's the only way to keep PACKAGE
dnl (i.e. the tarname) and VERSION dynamic at "./configure" runtime.

AC_SOURCE_VERSION(./VERSION)
AM_INIT_AUTOMAKE("$PACKAGE", "$VERSION")

dnl Required Autoconf version.
AC_PREREQ(2.53)

AC_REVISION([#ident "@(#)newsyslog:$Name:  $:$Id: configure.in,v 1.41 2002/08/23 04:56:46 woods Exp $"])

AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_LANG([C])

AC_PROG_MAKE_SET

AC_PROG_INSTALL

missing_dir=`cd $ac_aux_dir && pwd`
AM_MISSING_PROG(ACLOCAL, aclocal, $missing_dir)
AM_MISSING_PROG(AUTOCONF, autoconf, $missing_dir)
AM_MISSING_PROG(AUTOMAKE, automake, $missing_dir)
AM_MISSING_PROG(AUTOHEADER, autoheader, $missing_dir)
AM_MISSING_PROG(MAKEINFO, makeinfo, $missing_dir)

dnl Try to get a POSIX.1 environment.
dnl we need to call these system-specific macros early...
AC_AIX
AC_MINIX
AC_ISC_POSIX

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(errno.h string.h netdb.h sys/types.h sys/file.h sys/param.h unistd.h)

AC_CHECK_HEADERS(sys/user.h, , , [
#include <sys/types.h>
#include <sys/param.h>
])

AC_CHECK_HEADERS(sys/proc.h, , , [
#include <sys/types.h>
#include <sys/param.h>
#include <sys/user.h>
])

AC_CHECK_HEADERS(linux/tasks.h linux/threads.h)

AC_MSG_CHECKING(for sys/cdefs.h)
AC_TRY_CPP([
#include <sys/cdefs.h>
int main __P((int, char **));
], fd_cv_have_sys_cdefs_h=yes, fd_cv_have_sys_cdefs_h=no)
AC_MSG_RESULT($fd_cv_have_sys_cdefs_h)
if test $fd_cv_have_sys_cdefs_h = no ; then
	# ideally this would be '-I$(srcdir)/compat'
	CPPFLAGS="-Icompat $CPPFLAGS"
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

AC_TYPE_SIGNAL

AC_DECL_SYS_SIGNAME

dnl Checks for library functions.
AC_REPLACE_FUNCS(sig2str snprintf str2sig strdup strerror strtok strptime)

AC_CHECK_DECLS([sig2str, str2sig], [], [], [#include <signal.h>])
AC_CHECK_DECLS([strchr, strdup, strrchr, strtok], [], [], [#if HAVE_STRING_H
# include <string.h>
#endif
#if HAVE_STRINGS_H
# include <strings.h>
#endif])dnl

dnl STUPID GNU/Linux and/or glibc only declares strptime()
dnl if _XOPEN_SOURCE is defined!
AC_CHECK_DECLS(strptime, [], [], [#include <time.h>])

dnl Note that although bzero() is deprectated and non-standard we only
dnl need its capabilities, not those of the fancier and more standard
dnl memset(), so because bzero() can be defined in terms of memset()
dnl by a macro, but not the other way around, we use bzero() in the
dnl code and account for its absense with a macro.

dnl Also note that checking for flock() here doesn't guarantee a
dnl working flock()!

AC_CHECK_FUNCS(bzero flock isnan isinf memset mkstemp rename strchr strrchr)

AM_WITH_DMALLOC

dnl handle "--with-gzip"
AC_MSG_CHECKING(for name of archive compression program)

AC_ARG_WITH(gzip,
	AC_HELP_STRING([--with-gzip],
		[use GNU Zip instead of compress]),
	[COMPRESS_PROG=gzip
	COMPRESS_SUFFIX=".gz"],
	[COMPRESS_PROG=compress
	COMPRESS_SUFFIX=".Z"]
)

AC_MSG_RESULT($COMPRESS_PROG with $COMPRESS_SUFFIX)
AC_SUBST(COMPRESS_SUFFIX)
AC_DEFINE_UNQUOTED(COMPRESS_SUFFIX, "$COMPRESS_SUFFIX",
				    [Compressed file suffix string])

AC_PATH_PROG(PATH_COMPRESS, $COMPRESS_PROG)
AC_SUBST(PATH_COMPRESS)
AC_DEFINE_UNQUOTED(PATH_COMPRESS, "$PATH_COMPRESS",
				  [File compression program pathname])

dnl handle "--with-syslogd_pid=path"
AC_MSG_CHECKING(for name of syslogd's pid file)

case "$localstatedir" in
/*)
	expandedlocalstatedir=$localstatedir
	;;
*)
	oldprefix=$prefix
	if test ".$prefix" = .NONE ; then
		prefix=$ac_default_prefix
	fi
	expandedlocalstatedir=`eval echo $localstatedir`
	prefix=$oldprefix
	;;
esac

AC_ARG_WITH(syslogd_pid,
	AC_HELP_STRING([--with-syslogd_pid=path],
		 [syslogd's PID file (localstatedir/run/syslog.pid)]),
	[syslogd_pidfile=$withval],
	[syslogd_pidfile=$expandedlocalstatedir/run/syslog.pid]
)

AC_SUBST(syslogd_pidfile)
AC_DEFINE_UNQUOTED(PATH_SYSLOGD_PIDFILE, "$syslogd_pidfile",
					 [Pathname of syslogd's default PID file])
AC_MSG_RESULT($syslogd_pidfile)

dnl handle "--with-newsyslog_conf=path"
AC_MSG_CHECKING(for name of config file)

case "$sysconfdir" in
/*)
	expandedsysconfdir=$sysconfdir
	;;
*)
	oldprefix=$prefix
	if test ".$prefix" = .NONE ; then
		prefix=$ac_default_prefix
	fi
	expandedsysconfdir=`eval echo $sysconfdir`
	prefix=$oldprefix
	;;
esac

AC_ARG_WITH(newsyslog_conf,
	AC_HELP_STRING([--with-newsyslog_conf=path],
		[configuration file (sysconfdir/newsyslog.conf)]),
	[path_config=$withval],
	[path_config=$expandedsysconfdir/newsyslog.conf]
)

AC_SUBST(path_config)
AC_DEFINE_UNQUOTED(PATH_CONFIG, "$path_config",
				[Default path to the configuration file])
AC_MSG_RESULT($path_config)

dnl Files to create from their *.in templates:
AC_CONFIG_FILES([Makefile Makefile.BSD newsyslog.conf.5so newsyslog.8so])
AC_OUTPUT
