dnl
dnl Process this file with autoconf to produce a configure script.
dnl

dnl This file:  Copyright (C) 1997,1998,1999 Planix, Inc.
dnl             see COPYING for details

AC_INIT(newsyslog.c)

dnl Required Autoconf version.
AC_PREREQ(2.12)

AC_REVISION([#ident "@(#)newsyslog:$Name:  $:$Id: configure.in,v 1.31 2001/02/23 01:30:01 woods Exp $"])

dnl for Automake
PACKAGE="newsyslog"

AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_SUBST(PACKAGE)

. $srcdir/VERSION

changequote(,)
VERSION_MAJOR=`expr "$VERSION" : '^\([0-9]*\)'`
VERSION_MINOR=`expr "$VERSION" : '^[0-9]*\.\([0-9]*\)'`
VERSION_PATCH=`expr "$VERSION" : '^[0-9]*\.[0-9]*\.\([0-9]*\)'`
VERSION_SUFFIX=`expr "$VERSION" : '^[0-9]*\.[0-9]*\.[0-9]*-\([a-zA-Z0-9._,+]*\)'`
VERSION_COMMENT=`expr "$VERSION" : '^.*#\(.*\)'`
changequote([,])

AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_SUBST(VERSION)
AC_DEFINE_UNQUOTED(VERSION_MAJOR, $VERSION_MAJOR)
AC_SUBST(VERSION_MAJOR)
AC_DEFINE_UNQUOTED(VERSION_MINOR, $VERSION_MINOR)
AC_SUBST(VERSION_MINOR)
AC_DEFINE_UNQUOTED(VERSION_PATCH, $VERSION_PATCH)
AC_SUBST(VERSION_PATCH)
AC_DEFINE_UNQUOTED(VERSION_SUFFIX, "$VERSION_SUFFIX")
AC_SUBST(VERSION_SUFFIX)
AC_DEFINE_UNQUOTED(VERSION_COMMENT, "$VERSION_COMMENT")
AC_SUBST(VERSION_COMMENT)

AM_SANITY_CHECK

AC_ARG_PROGRAM

AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_LANG_C

AC_PROG_MAKE_SET

AC_PROG_INSTALL

missing_dir=`cd $ac_aux_dir && pwd`
AM_MISSING_PROG(ACLOCAL, aclocal, $missing_dir)
AM_MISSING_PROG(AUTOCONF, autoconf, $missing_dir)
AM_MISSING_PROG(AUTOMAKE, automake, $missing_dir)
AM_MISSING_PROG(AUTOHEADER, autoheader, $missing_dir)
AM_MISSING_PROG(MAKEINFO, makeinfo, $missing_dir)

dnl Try to get a POSIX.1 environment.
dnl we need to call these system-specific macros early...
AC_AIX
AC_MINIX
AC_ISC_POSIX

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(errno.h string.h sys/types.h unistd.h)

AC_MSG_CHECKING(for sys/cdefs.h)
AC_TRY_CPP([<sys/cdefs.h>], fd_cv_have_sys_cdefs_h=yes, fd_cv_have_sys_cdefs_h=no)
AC_MSG_RESULT($fd_cv_have_sys_cdefs_h)
if test $fd_cv_have_sys_cdefs_h = no ; then
	# ideally this would be '-I$(srcdir)/compat'
	CPPFLAGS="-Icompat $CPPFLAGS"
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

AC_TYPE_SIGNAL

dnl Checks for library functions.
AC_REPLACE_FUNCS(snprintf strdup strerror strtok strptime)

dnl Note that although bzero() is deprectated and non-standard we only
dnl need its capabilities, not those of the fancier and more standard
dnl memset(), so because bzero() can be defined in terms of memset()
dnl by a macro, but not the other way around, we use bzero() in the
dnl code and account for its absense with a macro.

dnl Also note that checking for flock() here doesn't guarantee a
dnl working flock()!

AC_CHECK_FUNCS(bzero strchr flock)

dnl handle "--with-gzip"
AC_MSG_CHECKING(for name of archive compression program)

AC_ARG_WITH(gzip,
	[  --with-gzip             use GNU Zip instead of compress],
	[COMPRESS_PROG=gzip
	COMPRESS_SUFFIX=".gz"],
	[COMPRESS_PROG=compress
	COMPRESS_SUFFIX=".Z"]
)

AC_MSG_RESULT($COMPRESS_PROG with $COMPRESS_SUFFIX)
AC_SUBST(COMPRESS_SUFFIX)
AC_DEFINE_UNQUOTED(COMPRESS_SUFFIX, "$COMPRESS_SUFFIX")

AC_PATH_PROG(PATH_COMPRESS, $COMPRESS_PROG)
AC_SUBST(PATH_COMPRESS)
AC_DEFINE_UNQUOTED(PATH_COMPRESS, "$PATH_COMPRESS")

dnl handle "--with-syslogd_pid=path"
AC_MSG_CHECKING(for name of syslogd's pid file)

case "$localstatedir" in
/*)
	expandedlocalstatedir=$localstatedir
	;;
*)
	oldprefix=$prefix
	if test ".$prefix" = .NONE ; then
		prefix=$ac_default_prefix
	fi
	expandedlocalstatedir=`eval echo $localstatedir`
	prefix=$oldprefix
	;;
esac

AC_ARG_WITH(syslogd_pid,
	[  --with-syslogd_pid=path syslogd's PID file (localstatedir/run/syslog.pid)],
	[syslogd_pidfile=$withval],
	[syslogd_pidfile=$expandedlocalstatedir/run/syslog.pid]
)

AC_SUBST(syslogd_pidfile)
AC_DEFINE_UNQUOTED(PATH_SYSLOGD_PIDFILE, "$syslogd_pidfile")
AC_MSG_RESULT($syslogd_pidfile)

dnl handle "--with-newsyslog_conf=path"
AC_MSG_CHECKING(for name of config file)

case "$sysconfdir" in
/*)
	expandedsysconfdir=$sysconfdir
	;;
*)
	oldprefix=$prefix
	if test ".$prefix" = .NONE ; then
		prefix=$ac_default_prefix
	fi
	expandedsysconfdir=`eval echo $sysconfdir`
	prefix=$oldprefix
	;;
esac

AC_ARG_WITH(newsyslog_conf,
	[  --with-newsyslog_conf=path configuration file (sysconfdir/newsyslog.conf)],
	[path_config=$withval],
	[path_config=$expandedsysconfdir/newsyslog.conf]
)

AC_SUBST(path_config)
AC_DEFINE_UNQUOTED(PATH_CONFIG, "$path_config")
AC_MSG_RESULT($path_config)

dnl Files to create from their *.in templates:
AC_OUTPUT([Makefile Makefile.BSD newsyslog.conf.5so newsyslog.8so])
